
FINISH
/clear,nostart
/output,a_output,txt 

/PREP7 
ET,1,BEAM188
KEYOPT,1,3,3


!Задание свойств материала
MP,EX,1,2.1e11
MP,PRXY,1,0.3
MP,DENS,1,7850

!Задание вида и размеров сечения
SECTYPE,1,BEAM,RECT
!Сечение балки
SECDATA,0.012,0.012


!Задание точек
K,1,0,0  
!Длина балки
K,2,0.9,0

!Создание линий между точками 
LSTR, 1, 2


!Разбиение сетки
LATT,1, ,1, , , ,1
!Размер сетки 5 см     
LESIZE,ALL,5e-2
LMESH,ALL


!Последний элемент
END_NODE = NODE(0.9,0,0)  
nsel,s,nodes,,END_NODE  
alls  


!Создание пружины
*GET,maxNode,nodes,0,num,max
*SET,maxNode,maxNode+1
n,maxNode,0.9,-(0.9/10),0
D,maxNode,ALL,0

!Пружина по игрек
et,2,combin14
keyopt,2,2,2
type,2 
real,1
e,maxNode,END_NODE

!Мю = 30, к = 70, с = 10
r,1,70,10


!Следящая сила
ET,3,FOLLW201
ALLSEL
!Присвоение эелемента
R,201,-1.0,0,0
REAL,201
type,3 
e,END_NODE
*get,followForce,elem,0,num,MAXD  

ALLSEL

NSEL,S,LOC,X,0
D,ALL,ALL
ALLSEL

D,ALL,UZ,0     

!Приложение силы
SFE,followForce,1,pres,1,448*30

!Решаем
/SOLU
NLGEOM,ON
nropt,unsym  
deltim,0.01,0.0001,0.1
outres,all,ALL
SOLVE
FINISH

/SOLU  
ANTYPE,,RESTART,,,PERTURB  
PERTURB,MODAL  
SOLVE,ELFORM  

MODOPT,DAMP,10  
MXPAND,10  
SOLVE

!Воводим
/POST1   
FILE,,RSTP   
*GET,RE_VAL,MODE,1,STAB   
*GET,IM_VAL,MODE,1,DFRQ   

*CREATE,final
*CFOPEN,'FORCE_RE_IM','TXT'
*VWRITE,30, RE_VAL, IM_VAL
(F20.10,' ',F20.10,' ',F20.10,' ',F20.10)    
*CFCLOS 
*END
/INPUT,final
